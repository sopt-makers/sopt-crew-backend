plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'jacoco'
}

group = 'org.sopt.makers.crew'
version = '1.1.2'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.1.5'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    //slack
    implementation 'com.slack.api:bolt:1.39.3'
    implementation 'com.slack.api:bolt-servlet:1.39.3'
    implementation 'com.slack.api:bolt-socket-mode:1.39.3'
    implementation 'com.slack.api:bolt-jetty:1.39.3'

    // WebSocket
    implementation 'javax.websocket:javax.websocket-api:1.1'
    implementation 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.19'


    // Lombok Dependencies (Compile-time code generation library)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // Development Tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:testcontainers:2.0.2'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.6'
    testImplementation 'org.testcontainers:postgresql:1.20.6'
    testImplementation 'org.testcontainers:jdbc:1.20.6'

    // Apache Commons Dependencies
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'

    // AOP (Aspect-Oriented Programming) for cross-cutting concerns
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Database and Persistence Dependencies
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // jsonb Type Handling
    implementation 'io.hypersistence:hypersistence-utils-hibernate-62:3.6.0'

    // JWT Dependencies for Security and Authentication
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
    implementation 'com.auth0:java-jwt:4.4.0'

    // H2 Database (for testing and in-memory DB)
    runtimeOnly 'com.h2database:h2'

    // macOS M1 Support (Network Resolver for macOS)
    implementation 'io.netty:netty-resolver-dns-native-macos:4.1.68.Final:osx-aarch_64'

    // Spring Cloud OpenFeign (Service-to-service communication)
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.0.3'

    // MapStruct (DTO transformation code generation)
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    // QueryDSL (JPA Query Generation)
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // AWS SDK for S3 (for file upload and download)
    implementation "software.amazon.awssdk:s3:2.27.0"

    // CSV Processing Library
    implementation 'com.opencsv:opencsv:5.5.2'

    // Prometheus (Monitoring and Metrics Collection)
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Sentry Logback (Error Tracking)
    implementation 'io.sentry:sentry-logback:7.17.0'

    // Slack Webhook
    implementation 'com.github.maricn:logback-slack-appender:1.4.0'

    // caffeine
    implementation("com.github.ben-manes.caffeine:caffeine:3.2.0")

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'

    // @ConfigurationProperties auto bean register
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // JSR305 (The compiler helps interpret null-safety-related annotations)
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Library for parsing JWT/JWK tokens and verifying their signatures
    implementation 'com.nimbusds:nimbus-jose-jwt:9.37.3'

}

tasks.named('test') {
    useJUnitPlatform()

    finalizedBy jacocoTestCoverageVerification

    jacoco {
        excludes += [
                "org/crew/main/MainApplication.class",
                "**/response/*",
                "**/config/*",
                "**/dto/*",
                "**/vo/*",
                "**/exception/*",
        ]
    }
}

ext {
    set('springCloudVersion', "2022.0.3")
}

dependencyManagement {

    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }

}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            "org/crew/main/MainApplication.class",
                            "**/response/*",
                            "**/config/*",
                            "org/crew/**/*Dto.*",
                            "**/dto/*",
                            "**/vo/*",
                            "**/exception/*",
                    ])
                })
        )
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            // ...

            excludes = [
                    '*.*Application',
                    '*.*Exception',
                    '*.Dto',
                    '*.response'
                    // ...
            ]
        }
    }
}
